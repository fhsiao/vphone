apply plugin: 'com.android.application'
apply plugin: 'testdroid'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion global_compileSdkVersion
    buildToolsVersion global_buildToolsVersion
    defaultConfig {
        applicationId "com.freetalk.vphone"
        minSdkVersion global_minSdkVersion
        targetSdkVersion global_targetSdkVersion
        versionCode global_versionCode
        versionName global_versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    dependencies {
        implementation fileTree(dir: 'libs', include: ['*.jar'])
        implementation 'com.android.support:design:27.1.+'
        implementation 'com.android.support:cardview-v7:27.1.+'
    }
    buildTypes {
        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                output.outputFileName = "freetalk/apps/vphone/vphone.apk"
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // Only one dimension but still required
    flavorDimensions "freetalk"
    productFlavors {
        // Generic SIP
        sip {
            applicationIdSuffix ""
            versionNameSuffix "-sip"
            dimension "freetalk"
            buildConfigField 'boolean', 'IS_SIP', 'true'
            buildConfigField 'boolean', 'IS_CISCO', 'false'
        }
        // Cisco extension support
        cisco {
            applicationIdSuffix ""
            versionNameSuffix "-cisco"
            dimension "freetalk"
            buildConfigField 'boolean', 'IS_SIP', 'false'
            buildConfigField 'boolean', 'IS_CISCO', 'true'
        }
    }
    sourceSets {
        main {
            res.srcDirs = ['src/main/res/']
        }
        debug {
            res.srcDirs = ['src/main/res/']
        }
    }
    // Enable cisco build by changing setIgnore to false
    variantFilter { variant ->
        def names = variant.flavors*.name
        if (names.contains("cisco")) {
            setIgnore(true)
        }
    }
    lintOptions {
        checkReleaseBuilds true
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    testdroid {
        username ''
        password ''
        deviceGroup 'vphone'
        mode "FULL_RUN"
    }
}

dependencies {
    implementation project(':pjapi')
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation global_appcompat
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    testImplementation global_testImplementation_junit_version
    androidTestImplementation global_testInstrumentationRunner
    androidTestImplementation(global_testInstrumentation_espresso_core, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
